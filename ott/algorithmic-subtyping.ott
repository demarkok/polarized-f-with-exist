embed {{ tex-preamble

\colorlet{colorA}{Purple}
\newcommand{\subA}{ \mathcolor{colorA}{ [[≤]] } } 
\newcommand{\supA}{ \mathcolor{colorA}{ [[≥]] } } 
}}

defns
  A :: 'A' ::=

  defn G ; Θ ⊨ uN ≤ iM ⫤ us :: :: Nsub :: ''
  {{ com Negative subtyping }}
  {{ tex [[G]] ;\, [[Θ]] [[⊨]] [[uN]] \subA [[iM]] [[⫤]] [[us]] }} by

  ----------------- :: NVar
  G;Θ ⊨ a⁻ ≤ a⁻ ⫤ · 

  Θ ⊨ nf(uP) ≈u nf(iQ) ⫤ us
  ---------------------------- :: ShiftU
  G;Θ ⊨ ↑uP ≤ ↑iQ ⫤ us

  G;Θ ⊨ uP ≥ iQ ⫤ us1    G;Θ ⊨ uN ≤ iM ⫤ us2
  ---------------------------------------- :: Arrow
  G;Θ ⊨ uP → uN ≤ iQ → iM ⫤ us1 & us2

  % *[G, pbs]
  G, pbs; Θ, â⁺*[G, pbs] ⊨ [â⁺*/pas] uN ≤ iM ⫤ us
  ----------------------------------- :: Forall
  G;Θ ⊨ ∀pas.uN ≤ ∀pbs.iM ⫤ us \ â⁺*

  defn G ; Θ ⊨ uP ≥ iQ ⫤ us :: :: Psup :: ''
  {{ com Positive supertyping }}
  {{ tex [[G]] ;\, [[Θ]] [[⊨]] [[uP]] \supA [[iQ]] [[⫤]] [[us]] }} by

  ---------------- :: PVar
  G;Θ ⊨ a⁺ ≥ a⁺ ⫤ ·

  Θ ⊨ nf(uN) ≈u nf(iM) ⫤ us
  ---------------------------- :: ShiftD
  G;Θ ⊨ ↓uN ≥ ↓iM ⫤ us

  % *[G, nbs]
  G, nbs; Θ,â⁻*[G, nbs] ⊨ [â⁻*/nas] uP ≥ iQ ⫤ us
  ------------------------------------------ :: Exists
  G;Θ ⊨ ∃nas.uP ≥ ∃nbs.iQ ⫤ us

    
   upgrade G ⊢ iP to Δ = iQ
  ----------------------------------------------- :: PUVar
  G;Θ ⊨ â⁺ [Δ] ≥ iP ⫤ (Δ ⊢ â⁺ :≥ iQ)
