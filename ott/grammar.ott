embed {{ tex-preamble

\usepackage{esvect}


% https://tex.stackexchange.com/questions/33401/a-version-of-colorbox-that-works-inside-math-environments
\setlength{\fboxsep}{1pt}
\newcommand{\ngbox}[1]{\mathchoice%
  {\colorbox{black!8}{$\displaystyle      \mathit{ #1 } $} }%
  {\colorbox{black!8}{$\textstyle         \mathit{ #1 } $} }%
  {\colorbox{black!8}{$\scriptstyle       \mathit{ #1 } $} }%
  {\colorbox{black!8}{$\scriptscriptstyle \mathit{ #1 } $} } }%

}}


metavar a {{ tex \alpha }}, b {{ tex \beta }}, α {{ tex \alpha }}, β {{ tex \beta }},
 γ {{ tex \gamma }},  δ {{ tex \delta }} ::= {{ com type variables }}

indexvar n , m, i , j ::= {{ com index variables }}


grammar

coh {{ tex n }}, mcoh {{ tex m }} :: 'n_' ::= {{ com cohort index }}
    | Z     :: :: Zero {{ tex 0 }}
    | S coh :: :: Succ {{ tex [[coh]]+1 }}


grammar

pa {{ tex \alpha^{+} }},
pb {{ tex \beta^{+} }},
pc {{ tex \gamma^{+} }},
pd {{ tex \delta^{+} }} 
:: 'pa_' ::= {{ com positive variable }}
   | a ⁺       :: :: Var {{ tex [[a]]^{+} }}
   | a ⁺ : coh :: :: VarCoh {{ tex [[a]]^{+ [[coh]] } }}
   | pnb       :: :: PNVar

na {{ tex \alpha^{-} }},
nb {{ tex \beta^{-} }},
nc {{ tex \gamma^{-} }},
nd {{ tex \delta^{-} }}
:: 'na_' ::= {{ com negative variable }}
   | a ⁻       :: :: Var {{ tex [[a]]^{-} }}
   | a ⁻ : coh :: :: VarCoh {{ tex [[a]]^{- [[coh]] } }}
   | pna       :: :: PNVar

pna {{ tex \alpha^{\pm} }},
pnb {{ tex \beta^{\pm} }}
:: 'pna_' ::= {{ com positive or negative variable }}
   | a ±       :: :: Var {{ tex [[a]]^{\pm} }}
   | a ± : coh :: :: VarCoh {{ tex [[a]]^{\pm [[coh]] } }}



grammar

s {{ tex \sigma }}, σ {{ tex \sigma }}  :: 's_' ::= {{ com substitution }}
   | ·                    ::   :: Empty
   | id                   ::   :: Id {{ tex \mathsf{id} }}
   | P / pa               ::   :: PosSub
   | N / na               ::   :: NegSub
   | iPs / pas            ::   :: PosSubs 
   | iNs / nas            ::   :: NegSubs
   | pmas / pas           ::   :: PosMSubs
   | nmas / nas           ::   :: NegMSubs
   | puas / pas           ::   :: PosUSubs
   | nuas / nas           ::   :: NegUSubs
   | nas / nuas           ::   :: NegFromUSubs
   | Sub μ                ::   :: FromMoving {{ tex [[μ]] }}
   | Sub us               ::   :: FromUs {{ tex [[us]] }}
   | σ1 ○ σ2              ::   :: Compose
   | ordVars1 / ordVars2  ::   :: VarSubs
   | ( σ )                :: S :: paren
   | </ si // , // i />   ::   :: concat  {{ com concatenate }}


usEntry  {{ tex e }} :: usE_' ::= {{ com entry of a unification solution }}
   | pua :≈ iP            :: :: PSol {{ tex [[pua]] :\approx   [[iP]] }}
   | nua :≈ iN            :: :: NSol {{ tex [[nua]] :\approx   [[iN]] }}
   | pua :≥ iP            :: :: PSup {{ tex [[pua]] :\geqslant [[iP]] }}
   % | nua :≤ iN            :: :: PSub {{ tex [[nua]] :[[≤]] [[iN]] }}
   | ( usEntry )              :: S :: paren
   | us ( pua )               :: M :: PSelect
   | us ( nua )               :: M :: NSelect

us {{ tex \widehat{\sigma} }}, uσ {{ tex \widehat{\sigma} }} :: 'us_' ::= {{ com unification solution (substitution) }}
   | ·                   ::   :: Empty
   | usEntry             ::   :: Entry
   | us \ varset         ::   :: Exclude
   | us | varset         ::   :: Restr
   | us1 ∪ us2           ::   :: Union
   | </ usi // , // i /> ::   :: concat  {{ com concatenate }}
   | ( us )              :: S :: paren

grammar

aus {{ tex \widehat{\tau} }} :: 'aus_' ::= {{ com anti-unification substitution }}
   | ·                    ::   :: Empty
   | nua :≈ iN            ::   :: NSol  {{ tex [[nua]] :\approx [[iN]] }}
   | nua :≈ uN            ::   :: NuSol {{ tex [[nua]] :\approx [[uN]] }}
   | nas / nuas           ::   :: NegFromUSubs
   | iNs / nuas           ::   :: Neg
   | aus1 ∪ aus2          ::   :: Union
   | </ ausi // , // i /> ::   :: concat  {{ com concatenate }}
   | ( aus )              :: S :: paren

grammar

P, Q :: 'P_' ::= {{ com positive types }}
   | pa       ::   :: Var
   | ↓ N      ::   :: Shift
   | ∃ na . P ::   :: Exists
   | [ s ] P  :: M :: AppSub 

N, M :: 'N_' ::= {{ com negative types }}
   | na       ::   :: Var
   | ↑ P      ::   :: Shift
   | ∀ pa . N ::   :: Forall
   | P → N    ::   :: Fun
   | [ s ] N  :: M :: AppSub 

grammar

pas {{ tex \vv{ \alpha^{+} } }},
pbs {{ tex \vv{ \beta^{+} } }},
pcs {{ tex \vv{ \gamma^{+} } }},
pds {{ tex \vv{ \delta^{+} } }}
:: 'pas_' ::= {{ com positive variable list }}
   | ·                    ::   :: empty   {{ com empty list }}
   | pa                   ::   :: Var     {{ com a variable }}
   | pa *                 ::   :: Vars    {{ com a variable }}
                                          {{ tex \vv{[[pa]]} }}
   | </ pasi // , // i /> ::   :: concat  {{ com concatenate lists }}

nas {{ tex \vv{ \alpha^{-} } }},
nbs {{ tex \vv{ \beta^{-} } }},
ncs {{ tex \vv{ \gamma^{-} } }},
nds {{ tex \vv{ \delta^{-} } }}
:: 'nas_' ::= {{ com negative variables }}
   | ·                    ::   :: empty   {{ com empty list }}
   | na                   ::   :: Var     {{ com a variable }}
   | na *                 ::   :: Vars    {{ com variables }}
                                          {{ tex \vv{[[na]]} }}
   | </ nasi // , // i /> ::   :: concat  {{ com concatenate lists }}

pnas {{ tex \vv{ \alpha^{\pm} } }},
pnbs {{ tex \vv{ \beta^{\pm} } }},
pncs {{ tex \vv{ \gamma^{\pm} } }},
pnds {{ tex \vv{ \delta^{\pm} } }}
:: 'pnas_' ::= {{ com positive or negative variable list }}
   | ·                     ::   :: empty   {{ com empty list }}
   | pna                   ::   :: Var     {{ com a variable }}
   | pna *                 ::   :: Vars    {{ com variables }}
                                           {{ tex \vv{[[pa]]} }}
   | </ pnasi // , // i /> ::   :: concat  {{ com concatenate lists }}


iP {{ tex P }}, iQ {{ tex Q }}:: 'iP_' ::= {{ com multi-quantified positive types }}
   | pa         ::   :: Var
   | ↓ iN       ::   :: Shift
   | ∃ nas . iP ::   :: Exists {{ com $[[iP]] \neq \exists\dots$ }}
   | [ s ]  iP  :: M :: AppSub  
   | [ aus ] uP :: M :: AppAUSub  
   | [ us ] uP  :: M :: AppUSub  
   | [ mu ] iP  :: M :: AppMoving 
   | ( iP )     :: S :: Paren

iN {{ tex N }}, iM {{ tex M }} :: 'iN_' ::= {{ com multi-quantified negative types }}
   | na          ::   :: Var
   | ↑ iP        ::   :: Shift
   | iP → iN     ::   :: Fun
   | ∀ pas . iN  ::   :: Forall {{ com $[[iN]] \neq \forall\dots$ }}
   | [ s ] iN    :: M :: AppSub 
   | [ mu ] iN   :: M :: AppMoving
   | [ us ] uN   :: M :: AppUSub  
   | ( iN )      :: S :: Paren


iPs {{ tex \vv{ P } }},
iQs {{ tex \vv{ Q } }}
:: 'iPs_' ::= {{ com list of positive types }}
   | ·                    ::   :: empty   {{ com empty list }}
   | iP                   ::   :: Var     {{ com a singel type }}
   | </ iPsi // , // i /> ::   :: concat  {{ com concatenate lists }}

iNs {{ tex \vv{ N } }},
iMs {{ tex \vv{ M } }}
:: 'iNs_' ::= {{ com list of negative types }}
   | ·                    ::   :: empty   {{ com empty list }}
   | iN                   ::   :: Var     {{ com a singel type }}
   | </ iNsi // , // i /> ::   :: concat  {{ com concatenate lists }}


grammar

G {{ tex \Gamma }}, Δ {{ tex \Delta }}, Γ {{ tex \Gamma }} :: DT_Ctxt ::= {{ com declarative type context }}
    | ·                  ::   :: empty   {{ com empty context }}
    % | varset             ::   :: nvar     {{ com a negative variable }}
    | pas                ::   :: PList   {{ com list of variables }}
    | nas                ::   :: NList   {{ com list of variables }}
    | pnas               ::   :: PNList  {{ com list of variables }}
    | Ord varset         ::   :: fromset {{ tex [[varset]] }}
    | </ Gi // , // i /> ::   :: concat  {{ com concatenate contexts }}
    | ( G )              :: S :: paren
    | Θ ( pua )          :: M :: pfromUC 
    | Θ ( nua )          :: M :: nfromUC 

grammar

T {{ tex \Theta }}, Θ {{ tex \Theta }}:: U_Ctxt ::= {{ com unification type variable context }}
    | ·                  ::   :: empty   {{ com empty context }}
    | puas               ::   :: PUList  {{ com list of variables }}
    | nuas               ::   :: NUList  {{ com list of variables }}
    | Ord varset         ::   :: fromset {{ tex [[varset]] }}
    | </ Θi // , // i /> ::   :: concat  {{ com concatenate contexts }}
    | ( Θ )              :: S :: paren
    | Θ | varset         ::   :: restr  {{ com leave only those variables that are in the set }}
                                        {{ tex [[Θ]]|_{[[varset]]} }}
    | Θ1 ∪ Θ2            ::   :: union
    % | Ξ ⁻                ::   :: NCtxt {{ tex [[Ξ]]^{-} }}
                                       % {{ com context containing only negative uvars }}

grammar

Xi {{ tex \Xi }}, Ξ {{ tex \Xi }} :: AU_Ctxt ::= {{ com anti-unification type variable context }}
    | ·                  ::   :: empty   {{ com empty context }}
    | puas               ::   :: PAUList {{ com list of variables }}
    | nuas               ::   :: NAUList {{ com list of variables }}
    | </ Ξi // , // i /> ::   :: concat  {{ com concatenate contexts }}
    | ( Ξ )              :: S :: paren
    | Ξ1 ∪ Ξ2            ::   :: union

                                       % {{ com context containing only negative uvars }}
grammar

ordVars {{ tex \vv{ \alpha } }}, ordVarsb {{ tex \vv{ \beta } }} :: OrdVars ::= {{ com ordered positive or negative variables }}
    | ·                         ::   :: empty     {{ com empty list }}
    | pas                       ::   :: PList     {{ com list of variables }}
    | nas                       ::   :: NList     {{ com list of variables }}
    | pnas                      ::   :: PNList    {{ com list of variables }}
    | puas                      ::   :: PUList    {{ com list of variables }}
    | nuas                      ::   :: NUList    {{ com list of variables }}
    | ordVars1 \ varset         ::   :: Setminus  {{ com setminus }}
    | G                         ::   :: Cntxt     {{ com context }}
    | Ord varset                ::   :: fromset   {{ tex [[varset]] }}
    | </ ordVarsi // , // i />  ::   :: concat    {{ com concatenate contexts }}
    | ( ordVars )               :: S :: paren     {{ com parenthesis }}
    | [ mu ] ordVars            :: M :: moving    {{ com apply moving to list }}


grammar

varset {{ tex \textit{vars} }} :: Varset ::= {{ com set of variables }}
    | ∅                  ::   :: emptyset  {{ com empty set }}
    | fv iP              ::   :: Pfv       {{ com free variables }}
    | fv iN              ::   :: Nfv       {{ com free variables }}
    | fv imP             ::   :: mPfv      {{ com free variables }}
    | fv imN             ::   :: mNfv      {{ com free variables }}
    | varset1 ∩ varset2  ::   :: intersect {{ com set intersection }}
    | varset1 ∪ varset2  ::   :: union     {{ com set union }}
    | varset1 \ varset2  ::   :: setminus  {{ com set complement }}
    | mv imP             ::   :: Pmv       {{ com movable variables }}
    | mv imN             ::   :: Nmv       {{ com movable variables }}
    | uv uN              ::   :: Nuv       {{ com unification variables }}
    | uv uP              ::   :: Puv       {{ com unification variables }}
    | fv uN              ::   :: uNfv      {{ com free variables }}
    | fv uP              ::   :: uPfv      {{ com free variables }}
    | ( varset )         :: S :: paren     {{ com parenthesis }}
    | { ordVars }        ::   :: OrdVars   {{ com ordered list of variables }}
                                           {{ tex [[ordVars]] }}
    | [ mu ] varset      :: M :: moving    {{ com apply moving to varset }}
grammar

mu {{ tex \mu }}, μ {{ tex \mu }}:: Mu ::= 
    | ·                   ::   :: empty   {{ com empty moving }}
    | pma1 ↦ pma2         ::   :: Psingle {{ com Positive unit substitution }}
    | nma1 ↦ nma2         ::   :: Nsingle {{ com Positive unit substitution }}
    | mu1 ∪ mu2           :: M :: Union   {{ com Set-like union of movings }} 
    | mu1 ○ mu2           :: M :: Compose {{ com Composition }} 
    | </ mui // , // i /> ::   :: concat  {{ com concatenate movings }}
    | mu |_ varset        :: M :: restr   {{ com restriction on a set }} 
                                          {{ tex [[mu]]|_{[[varset]]}  }} 
    | mu -1               :: M :: inv     {{ com inversion }} 
                                          {{ tex [[mu]]^{-1} }} 


% pma {{ tex \widetilde{\alpha}^{+} }} :: pma_ ::= {{ com positive movable variable }}
%     | a + : coh :: :: Var {{ tex \widetilde{[[a]]}^{+ [[coh]] } }}


% nma {{ tex \widetilde{\alpha}^{-} }} :: nma_ ::= {{ com negative movable variable }}
%     | a - : coh :: :: Var {{ tex \widetilde{[[a]]}^{- [[coh]] } }}


% pmas {{ tex \vv{ \widetilde{ \alpha^{+} } } }},
% pmbs {{ tex \vv{ \widetilde{ \beta^{+}  } } }}
% :: 'pmas_' ::= {{ com positive movable variable list }}
%    | ·                     ::   :: empty    {{ com empty list }}
%    | pma                   ::   :: Var      {{ com a variable }}
%    % | pas : coh             ::   :: FromVars {{ com from a list of non-movable variables }}
%    %                                          {{ tex \vv{ \widetilde{ [[pas]]^{+[[coh]]} } } }}
%    | a + * : coh            ::   :: FromVar  {{ com from a non-movable variable }}
%                                             {{ tex \vv{ \widetilde{ [[a]]^{+[[coh]]} } } }}
%    | </ pmasi // , // i /> ::   :: concat   {{ com concatenate lists }}

% nmas {{ tex \vv{ \widetilde{ \alpha^{-} } } }},
% nmbs {{ tex \vv{ \widetilde{ \beta^{-}  } } }}
% :: 'nmas_' ::= {{ com negatiive movable variable list }}
%    | ·                     ::   :: empty    {{ com empty list }}
%    | nma                   ::   :: Var      {{ com a variable }}
%    % | nas : coh             ::   :: FromVars {{ com transofmed non-movable variables }}
%    %                                          {{ tex \vv{ \widetilde{ [[nas]]^{-[[coh]]} } } }}
%    | a - * : coh            ::   :: FromVar  {{ com from a non-movable variable }}
%                                             {{ tex \vv{ \widetilde{ [[a]]^{-[[coh]]} } } }}
%    | </ nmasi // , // i /> ::   :: concat   {{ com concatenate lists }}


% imP {{ tex P }}, imQ {{ tex Q }}:: 'imP_' ::= {{ com multi-quantified positive types with movable variables }}
%    | pa          ::   :: Var
%    | pma         ::   :: MovVar
%    | ↓ imN       ::   :: Shift
%    | ∃ nas . imP ::   :: Exists
%    | [ s ] imP   :: M :: AppSub  
%    | [ mu ] imP  :: M :: AppMoving 

% imN {{ tex N }}, imM {{ tex M }} :: 'imN_' ::= {{ com multi-quantified negative types with movable variables }}
%    | na          ::   :: Var
%    | nma         ::   :: MovVar
%    | ↑ imP       ::   :: Shift
%    | imP → imN    ::   :: Fun
%    | ∀ pas . imN ::   :: Forall
%    | [ s ] imN   :: M :: AppSub 
%    | [ mu ] imN  :: M :: AppMoving



grammar

pnua {{ tex \widehat{\alpha}^{\pm} }} :: pnua_ ::= {{ com positive/negative unification variable }}
    | a ̂ ±        :: :: Var {{ tex \widehat{[[a]]}^{\pm} }}
    %| a ̂ ± [ Δ ]  :: :: AnnotVar {{ tex \widehat{[[a]]}^{\pm}\{ [[Δ]] \} }}

pua {{ tex \widehat{\alpha}^{+} }} :: pua_ ::= {{ com positive unification variable }}
    | a ̂ ⁺        :: :: Var {{ tex \widehat{[[a]]}^{+} }}
    | pua  [ Δ ]  :: :: AnnotVar {{ tex [[pua]]\{ [[Δ]] \} }}
    | pnua        :: :: PN

nua {{ tex \widehat{\alpha}^{-} }},
nub {{ tex \widehat{\beta}^{-} }}
:: nua_ ::= {{ com negative unification variable }}
    | a ̂ ⁻               :: :: Var {{ tex \widehat{[[a]]}^{-} }}
    | a ̂ ⁻ _ { iN , iM } :: :: VarIdxd {{ tex \widehat{[[a]]}^{-}_{\{ [[iN]], [[iM]] \} } }}
    | nua [ Δ ]          :: :: AnnotVar {{ tex [[nua]]\{ [[Δ]] \} }}
    | pnua               :: :: PN

puas {{ tex \vv{ \widetilde{ \alpha^{+} } } }},
pubs {{ tex \vv{ \widetilde{ \beta^{+}  } } }}
:: 'puas_' ::= {{ com positive unification variable list }}
   | ·                     ::   :: empty    {{ com empty list }}
   | pua                   ::   :: Var      {{ com a variable }}
   | pua * [ Δ ]           ::   :: FromVarC {{ com from a normal variable }}
                                            {{ tex \vv{ [[pua]] \{[[Δ]]\} } }}
   | pua *                 ::   :: FromVar  {{ com from a normal variable, context unspecified }}
                                            {{ tex \vv{ [[pua]] } }}
   | </ puasi // , // i /> ::   :: concat   {{ com concatenate lists }}

nuas {{ tex \vv{ \widehat{ \alpha^{-} } } }},
nubs {{ tex \vv{ \widehat{ \beta^{-}  } } }}
:: 'nuas_' ::= {{ com negative unification variable list }}
   | ·                     ::   :: empty    {{ com empty list }}
   | nua                   ::   :: Var      {{ com a variable }}
   | ToList Ξ              ::   :: Ctxt     {{ com from an antiunification context }}
                                            {{ tex [[Ξ]] }}
   | nua * [ Δ ]           ::   :: FromVarC {{ com from a normal variable }}
                                            {{ tex \vv{ [[nua]] \{[[Δ]]\} } }}
   | nua *                 ::   :: FromVar  {{ com from a normal variable, context unspecified }}
                                            {{ tex \vv{ [[nua]]  } }}
   | </ nuasi // , // i /> ::   :: concat   {{ com concatenate lists }}



uP {{ tex \ngbox{P} }}, uQ {{ tex \ngbox{Q} }}:: 'uP_' ::= {{ com a positive algorithmic type (potentially with metavariables) }}
                                                            {{ tex \ngbox{[[uP]]} }} | pa             ::   :: Var | pma            ::   :: MovVar
   | pua           ::   :: UnifVar 
   | ↓ uN          ::   :: Shift
   | ∃ nas . uP    ::   :: Exists
   | [ s ] uP      :: M :: AppSub  
   | [ aus ] uP    :: M :: AppUSub  
   | [ mu ] uP     :: M :: AppMoving 
   | ( uP )        :: S :: Paren

uN {{ tex \ngbox{N} }}, uM {{ tex \ngbox{M} }} :: 'uN_' ::= {{ com a negative algorithmic type (potentially with metavariables) }}
                                                             {{ tex \ngbox{[[uN]]} }}
   | na             ::   :: Var
   | nua            ::   :: UnifVar
   | ↑ uP           ::   :: Shift
   | uP → uN        ::   :: Fun
   | ∀ pas . uN     ::   :: Forall
   | [ s ] uN       :: M :: AppSub 
   | [ mu ] uN      :: M :: AppMoving
   | ( uN )         :: S :: Paren


auSol :: 'auSOL_' ::=
   | ( Ξ , uQ , aus1 , aus2 ) :: :: Sol

terminals :: 'terminals_' ::=
  | ∃  :: :: exists    {{ tex \exists }}
  | ∀  :: :: forall    {{ tex \forall }}
  | ↑  :: :: upShift   {{ tex \mathord{\uparrow} }}
  | ↓  :: :: downShift {{ tex \mathord{\downarrow} }}
  | →  :: :: funArrow  {{ tex \rightarrow }}
  | ↔  :: :: bijArrow  {{ tex \leftrightarrow }}
  | ∊  :: :: inSet     {{ tex \in }}
  | ∉  :: :: notInSet  {{ tex \notin }}
  | ·  :: :: cDot      {{ tex \cdot }}
  | ⊢  :: :: vdash     {{ tex \vdash }}
  | ≤  :: :: subtype   {{ tex \leqslant }}
  | ≥  :: :: supertype {{ tex \geqslant }}
  | ≈  :: :: equiv     {{ tex \simeq }}
  | ∪  :: :: union     {{ tex \cup }}
  | ∩  :: :: intersect {{ tex \cap }}
  | \  :: :: setminus  {{ tex \setminus }}
  | ⊆  :: :: subset    {{ tex \subseteq }}
  | ↦  :: :: mapsto    {{ tex \mapsto }}
  | ≈u :: :: unify     {{ tex \overset{u}{\simeq} }}
  | ≈au :: :: antiunify  {{ tex \overset{a}{\simeq} }}
  | ∅ :: :: empty      {{ tex \emptyset }}
  | ○ :: :: compose    {{ tex \circ }}
  | ⇒ :: :: implies    {{ tex \Rightarrow }}
  % | ≈a :: :: antiunify {{ tex \overset{g}{\simeq} }}
  | ⊨  :: :: vDash     {{ tex \vDash }}
  | ⫤  :: :: Dashv     {{ tex \Dashv }}
  % | ⊫  :: :: VDash     {{ tex \mathcolor{\ccolor}{\VDash} }}
  % | ⫥  :: :: DashV     {{ tex \mathcolor{\ccolor}{\DashV} }}
  | ≠  :: :: neq       {{ tex \neq }}
  % | ≡  :: :: equiv     {{ tex \mathcolor{\rcolor}{\equiv} }}
  | ≡n  :: :: equivn    {{ tex \equiv_{n} }}
  | ∨  :: :: LUB       {{ tex \vee }}
  | ⇓ :: :: norm       {{ tex \Downarrow }}
  | :≥ :: :: solGeq    {{ tex :[[≥]] }}
  | :≈ :: :: solEq     {{ tex :[[≈]] }}


% subrules
%   iP <:: imP
%   iN <:: imN
% subrules
%   iP <:: uP  
%   iN <:: uN  


funs
  LUBF ::=
% fun
%   iP1 ∨ iP2 :: iQ :: LUB 
% by

fun ord varset in uP :: ordVars :: uPOrd by
fun ord varset in uN :: ordVars :: uNOrd by
fun ord varset in iP :: ordVars :: NOrd by
fun ord varset in iN :: ordVars :: POrd by

fun nf ( iN' ) :: iN :: Nnf by
fun nf ( iP' ) :: iP :: Pnf by
fun nf ( uN' ) :: uN :: uNnf by
fun nf ( uP' ) :: uP :: uPnf by
fun nf ( iNs' ) :: iNs :: uNsnf by
fun nf ( iPs' ) :: iPs :: uPsnf by
fun nf ( σ' )   :: σ :: Snf by
fun nf ( μ' )   :: μ :: Movnf by
fun nf ( us' )  :: us :: USolnf by

fun  σ' | varset  :: σ :: Restr {{ tex [[σ']]|_{[[varset]]} }} by 

fun
  usEntry1 & usEntry2 :: usEntry :: USEM {{ tex [[usEntry1]] ~ \& ~ [[usEntry2]] }}
by

fun
  us1 & us2 :: us :: MRG {{ tex [[us1]] ~ \& ~[[us2]] }}
by

fun  dom ( us ) :: varset :: usolDom by

fun  dom ( Θ ) :: varset :: uCtxtDom by
