
embed {{ tex-preamble

\colorlet{colorU}{Purple!90!Black}
\newcommand{\eqU}{ \mathcolor{colorU}{ [[≈u]] } } 
% \newcommand{\subEOneA}{ \mathcolor{colorEOneA}{ [[≤]]_0^A } } 
% \newcomma

}}


defns
  U :: 'U' ::=

  defn Θ ⊨ uN ≈u iM ⫤ us :: :: NUnif :: ''
  {{ com Negative unification }}
  {{ tex [[Θ]] \vDash [[uN]] \eqU [[iM]] \Dashv [[us]] }} by

  -------------------- :: NVar
  Θ ⊨ a⁻ ≈u a⁻ ⫤ · 


  Θ ⊨ uP ≈u iQ ⫤ us
  --------------------- :: ShiftU
  Θ ⊨ ↑uP ≈u ↑iQ ⫤ us

  Θ ⊨  uP ≈u iQ ⫤ us1    Θ ⊨ uN ≈u iM ⫤ us2
  -------------------------------- :: Arrow
  Θ ⊨  uP → uN ≈u iQ → iM ⫤ us1 & us2

  Θ ⊨  uN ≈u iM ⫤ us
  ---------------------------- :: Forall
  Θ ⊨  ∀pas.uN ≈u ∀pas.iM ⫤ us

  â⁻[Δ] ∊ Θ   Δ ⊢ iN  
  ------------------------------------- :: NUVar
  Θ ⊨  â⁻ ≈u iN ⫤ (Δ ⊢ â⁻ :≈ iN)

  defn Θ ⊨ uP ≈u iQ ⫤ us :: :: PUnif :: ''
  {{ com Positive unification }}
  {{ tex [[Θ]] \vDash [[uP]] \eqU [[iQ]] \Dashv [[us]] }} by

  ---------------------  :: PVar
  Θ ⊨  a⁺ ≈u a⁺ ⫤ ·

  Θ ⊨  uN ≈u iM ⫤ us
  -------------------------- :: ShiftD
  Θ ⊨  ↓uN ≈u ↓iM ⫤ us

  Θ ⊨  uP ≈u iQ ⫤ us
  ------------------------ :: Exists 
  Θ ⊨  ∃nas.uP ≈u ∃nas.iQ ⫤ us

  â⁺[Δ] ∊ Θ   Δ ⊢ iP
  --------------------------------- :: PUVar
  Θ ⊨  â⁺ ≈u iP ⫤ (Δ ⊢ â⁺ :≈ iP)