embed {{ tex-preamble

\colorlet{colorDZero}{OliveGreen}
\newcommand{\eqDZero}{ \mathcolor{colorDZero}{ [[≈]]_0^{ \scaleto{[[≤]]}{3.5pt} } } } 
\newcommand{\subDZero}{ \mathcolor{colorDZero}{ [[≤]]_0 } } 
\newcommand{\supDZero}{ \mathcolor{colorDZero}{ [[≥]]_0 } } 

}}

defns
  D0 :: 'D0' ::=

  defn G ⊢ N ≈₀ M :: :: Neq :: ''
  {{ com Negative equivalence }}
  {{ tex [[G]] [[⊢]] [[N]] \eqDZero [[M]] }} by

  G ⊢ N ≤₀ M   G ⊢ M ≤₀ N
  --------------------- :: NDef
  G ⊢ N ≈₀ M 

  defn G ⊢ P ≈₀ Q :: :: Peq :: ''
  {{ com Positive equivalence }}
  {{ tex [[G]] [[⊢]] [[P]]  \eqDZero [[Q]] }} by

  G ⊢ P ≥₀ Q   G ⊢ Q ≥₀ P
  --------------------- :: PDef
  G ⊢ P ≈₀ Q 

  defn G ⊢ N ≤₀ M :: :: Nsub :: ''
  {{ com Negative subtyping }}
  {{ tex [[G]] [[⊢]] [[N]] \subDZero [[M]] }} by

  ----------- :: NVar
  G ⊢ a⁻ ≤₀ a⁻ 

  G ⊢ P ≈₀ Q
  ------------ :: ShiftU
  G ⊢ ↑P ≤₀ ↑Q

  G ⊢ P   G ⊢ [P/a⁺] N ≤₀ M    M ≠ ∀b⁺.M'
  ------------------------------------- :: ForallL
  G ⊢ ∀a⁺.N ≤₀ M

  G, a⁺ ⊢ N ≤₀ M
  -------------- :: ForallR
  G ⊢ N ≤₀ ∀a⁺.M

  G ⊢ P ≥₀ Q    G ⊢ N ≤₀ M 
  ----------------------- :: Arrow
  G ⊢ P → N ≤₀ Q → M

  defn G ⊢ P ≥₀ Q :: :: Psup :: ''
  {{ com Positive supertyping }}
  {{ tex [[G]] [[⊢]] [[P]] \supDZero [[Q]] }} by

  ----------- :: PVar
  G ⊢ a⁺ ≥₀ a⁺ 

  G ⊢ N ≈₀ M
  ------------ :: ShiftD
  G ⊢ ↓N ≥₀ ↓M

  G ⊢ N   G ⊢ [N/a⁻] P ≥₀ Q    Q ≠ ∃a⁻.Q'
  -------------------------------------- :: ExistsL
  G ⊢ ∃a⁻.P ≥₀ Q

  G, a⁻ ⊢ P ≥₀ Q
  --------------- :: ExistsR
  G ⊢ P ≥₀ ∃a⁻.Q
