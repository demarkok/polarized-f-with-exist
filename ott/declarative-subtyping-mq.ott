embed {{ tex-preamble

\colorlet{colorOne}{Yellow!50!Brown}
\newcommand{\eqDOne}{ \mathcolor{colorOne}{ [[≈]]_1^{ \scaleto{[[≤]]}{3.5pt} } } } 
\newcommand{\subDOne}{ \mathcolor{colorOne}{ [[≤]]_1 } } 
\newcommand{\supDOne}{ \mathcolor{colorOne}{ [[≥]]_1 } } 

}}

defns
  D1 :: 'D1' ::=

  defn G ⊢ iN ≈ iM :: :: Neq :: ''
  {{ com Negative equivalence on MQ types }}
  {{ tex [[G]] [[⊢]] [[iN]] \eqDOne [[iM]] }} by

  G ⊢ iN ≤ iM   G ⊢ iM ≤ iN
  ------------------------- :: NDef
  G ⊢ iN ≈ iM 

  defn G ⊢ iP ≈ iQ :: :: Peq :: ''
  {{ com Positive equivalence on MQ types }}
  {{ tex [[G]] [[⊢]] [[iP]] \eqDOne [[iQ]] }} by

  G ⊢ iP ≥ iQ   G ⊢ iQ ≥ iP
  --------------------- :: PDef
  G ⊢ iP ≈ iQ 

  defn G ⊢ iN ≤ iM :: :: Nsub :: ''
  {{ com Negative subtyping }}
  {{ tex [[G]] [[⊢]] [[iN]] \subDOne [[iM]] }} by

  ----------- :: NVar
  G ⊢ a⁻ ≤ a⁻ 

  G ⊢ iP ≈ iQ
  ------------ :: ShiftU
  G ⊢ ↑iP ≤ ↑iQ

  G ⊢ iP ≥ iQ    G ⊢ iN ≤ iM 
  -------------------------- :: Arrow
  G ⊢ iP → iN ≤ iQ → iM

  Γ, pbs ⊢ σ : pas    G, pbs ⊢ [σ] iN ≤ iM
  ---------------------------------------- :: Forall
  G ⊢ ∀pas.iN ≤ ∀pbs.iM

  defn G ⊢ iP ≥ iQ :: :: Psup :: ''
  {{ com Positive supertyping }}
  {{ tex [[G]] [[⊢]] [[iP]] \supDOne [[iQ]] }} by

  ------------- :: PVar
  G ⊢ a⁺ ≥ a⁺ 

  G ⊢ iN ≈ iM
  -------------- :: ShiftD
  G ⊢ ↓iN ≥ ↓iM

  G, nbs ⊢ σ : nas    G, nbs ⊢ [σ]iP ≥ iQ
  --------------------------------------- :: Exists
  G ⊢ ∃nas.iP ≥ ∃nbs.iQ

  defn G2 ⊢ σ1 ≈ σ2 : G1 :: :: SEq :: ''
  {{ com Equivalence of substitutions }}
  {{ tex [[G2]] [[⊢]] [[σ1]] \eqDOne [[σ2]] : [[G1]] }} by


  defn Γ ⊢ σ1 ≈ σ2 : varset :: :: SubsEq :: 'SubsEq'
  {{ com Equivalence of substitutions }}
  {{ tex [[Γ]] [[⊢]] [[σ1]] \eqDOne [[σ2]] : [[varset]] }} by

  defn Θ ⊢ uσ1 ≈ uσ2 : varset :: :: USubsEq :: 'USubsEq'
  {{ com Equivalence of unification substitutions }}
  {{ tex [[Θ]] [[⊢]] [[uσ1]] \eqDOne [[uσ2]] : [[varset]] }} by

  defn Γ ⊢ uσ1 ≈ uσ2 : varset :: :: USubsEqG :: 'USubsEqG'
  {{ com Equivalence of unification substitutions }}
  {{ tex [[Γ]] [[⊢]] [[uσ1]] \eqDOne [[uσ2]] : [[varset]] }} by

  defn Γ ⊢ Φ1 ≈ Φ2 :: :: CtxEq :: 'CtxEq'
  {{ com Equivalence of contexts }}
  {{ tex [[Γ]] [[⊢]] [[Φ1]] \eqDOne [[Φ2]] }} by
